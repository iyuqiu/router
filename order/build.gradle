if (isRelease) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'

android {
    namespace 'com.example.order'
    compileSdk Integer.parseInt(libs.versions.compileSdk.get())

    defaultConfig {
        if (!isRelease) {
            applicationId = "com.example.order"
            targetSdk = Integer.parseInt(libs.versions.targetSdk.get())
            versionCode = Integer.parseInt(libs.versions.versionCode.get())
            versionName = libs.versions.versionName.get()
        }
        minSdk = Integer.parseInt(libs.versions.minSdk.get())
        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName(), packageNameForAPT: packageNameForAPT]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    viewBinding {
        enabled = true
    }

    sourceSets {
        main {
            if (!isRelease) {
                manifest.srcFile "src/main/debug/AndroidManifest.xml"
            } else {
                manifest.srcFile "src/main/AndroidManifest.xml"
                java {
                    exclude '**debug**'
                }
            }
        }
    }
}

dependencies {
    implementation project(':common')
    implementation project(':annotation')
    kapt project(':compiler')
    implementation(libs.bundles.appcompatBundles)
    implementation(libs.bundles.widgetBundles)
}